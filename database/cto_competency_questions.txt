# ----------------------------------------------------------------------------
# QUERY 1: Quel est le dénivelé total cumulé pour chaque 'Tour Package' et quel est le guide responsable de ce tour ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX cto: <http://data.cyclingtour.fr/data#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

SELECT ?tourLabel ?guideName (SUM(?elevation) AS ?totalElevation)
WHERE {
  ?tour a cs:TourPackage ;
        rdfs:label ?tourLabel ;
        cs:guideAssigned ?guide ;
        cs:includesStage ?stage .

  ?guide foaf:name ?guideName .

  ?stage cs:stagePath ?path .
  ?path cs:elevationGain ?elevation . 

FILTER (?elevation > 0)
}
GROUP BY ?tourLabel ?guideName
ORDER BY DESC(?totalElevation)

# ----------------------------------------------------------------------------
# QUERY 2: Quels 'Tour Packages' ont des étapes avec une difficulté 'Hard' ou 'Very Hard', mais sans étapes 'Easy', et quel est le prix moyen par jour ainsi que le nombre de clients ayant réservé ces tours ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?tourLabel ?price (AVG(?elevation) AS ?averageElevation) (COUNT(DISTINCT ?client) AS ?nbClients)
WHERE {
  ?tour a cs:TourPackage ;
        rdfs:label ?tourLabel ;
        cs:pricePerDayTour ?price .

  ?tour cs:includesStage/cs:stagePath/cs:difficulty ?difficulty .
  FILTER (?difficulty IN (cs:Hard, cs:VeryHard))

  FILTER NOT EXISTS {
    ?tour cs:includesStage ?stageEasy .
    ?stageEasy cs:stagePath ?pathEasy .
    ?pathEasy cs:difficulty cs:Easy .
  }

  ?tour cs:includesStage ?stage .
  ?stage cs:stagePath ?path .
  ?path cs:elevationGain ?elevation .

  FILTER (?elevation > 0)

  OPTIONAL {
    ?booking cs:tourPackageBooked ?tour ;
             cs:bookedBy ?client .
  }
}
GROUP BY ?tourLabel ?price

# ----------------------------------------------------------------------------
# QUERY 3: Quels sont les "Couples de Géants", des montagnes de haute altitude (> 1000m) qui sont directement reliées entre elles, et quel est le dénivelé qu'il faudrait pour passer de l'une à l'autre ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX cto: <http://data.cyclingtour.fr/data#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT 
  ?massif 
  ?sommet1 
  ?alt1 
  ?sommet2 
  ?alt2 
  ?ecartAltitude
  ?statut
WHERE {
  ?m1 a cs:Mountain ;
      rdfs:label ?sommet1 ;
      cs:elevation ?alt1 ;
      cs:mountainRange ?rangeUri .

  ?m1 cs:isNear ?m2 .

  ?m2 a cs:Mountain ;
      rdfs:label ?sommet2 ;
      cs:elevation ?alt2 .

  FILTER (?alt1 > 1000 && ?alt2 > 1000 && ?alt1 > ?alt2)
  BIND ((?alt1 - ?alt2) AS ?ecartAltitude)
  BIND (
    IF(?ecartAltitude < 100, "Traversée Plane", 
    IF(?ecartAltitude < 500, "Vallonné", "Casse-Pattes")) 
    AS ?statut
  )
  BIND (REPLACE(STR(?rangeUri), "^.*/", "") AS ?massif)
}
ORDER BY DESC(?alt1)
LIMIT 15

