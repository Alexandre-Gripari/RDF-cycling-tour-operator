# ----------------------------------------------------------------------------
# QUERY 1: Quel est le dénivelé total cumulé pour chaque 'Tour Package' et quel est le guide responsable de ce tour ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX cto: <http://data.cyclingtour.fr/data#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

SELECT ?tourLabel ?guideName (SUM(?elevation) AS ?totalElevation)
WHERE {
  ?tour a cs:TourPackage ;
        rdfs:label ?tourLabel ;
        cs:guideAssigned ?guide ;
        cs:includesStage ?stage .

  ?guide foaf:name ?guideName .

  ?stage cs:stagePath ?path .
  ?path cs:elevationGain ?elevation . 

FILTER (?elevation > 0)
}
GROUP BY ?tourLabel ?guideName
ORDER BY DESC(?totalElevation)

# ----------------------------------------------------------------------------
# QUERY 2: Quels 'Tour Packages' ont des étapes avec une difficulté 'Hard' ou 'Very Hard', mais sans étapes 'Easy', et quel est le prix moyen par jour ainsi que le nombre de clients ayant réservé ces tours ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?tourLabel ?price (AVG(?elevation) AS ?averageElevation) (COUNT(DISTINCT ?client) AS ?nbClients)
WHERE {
  # Identification du Tour et du prix
  ?tour a cs:TourPackage ;
        rdfs:label ?tourLabel ;
        cs:pricePerDayTour ?price .

  ?tour cs:includesStage/cs:stagePath/cs:difficulty ?difficulty .
  FILTER (?difficulty IN (cs:Hard, cs:VeryHard))

  FILTER NOT EXISTS {
    ?tour cs:includesStage ?stageEasy .
    ?stageEasy cs:stagePath ?pathEasy .
    ?pathEasy cs:difficulty cs:Easy .
  }

  ?tour cs:includesStage ?stage .
  ?stage cs:stagePath ?path .
  ?path cs:elevationGain ?elevation .

  FILTER (?elevation > 0)

  OPTIONAL {
    ?booking cs:tourPackageBooked ?tour ;
             cs:bookedBy ?client .
  }
}
GROUP BY ?tourLabel ?price

# ----------------------------------------------------------------------------
# QUERY 3: Quels guides sont assignés à des étapes longues (longueur > 50 km) et quel est le prix journalier du tour associé ?
# ----------------------------------------------------------------------------

PREFIX cs: <http://data.cyclingtour.fr/schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?guideName ?stageLabel ?length ?tourPrice
WHERE {
  ?stage a cs:TourStage ;
         cs:guideAssigned ?guide ;
         cs:stagePath ?path . 

  ?path cs:length ?length .
  
  FILTER (?length > 50.0)

  ?tour cs:includesStage ?stage ;
        cs:pricePerDayTour ?tourPrice .

  OPTIONAL { ?guide rdfs:label ?guideName } . 
  OPTIONAL { ?guide foaf:name ?guideName } .
  OPTIONAL { ?stage rdfs:label ?stageLabel } .
}
ORDER BY DESC(?tourPrice)

